/*
 * Copyright 2023 wcaokaze
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

pluginManagement {
   repositories {
      gradlePluginPortal()
      google()
      mavenCentral()
      maven { url 'https://maven.pkg.jetbrains.space/public/p/compose/dev' }
   }
}

dependencyResolutionManagement {
   repositoriesMode.set(RepositoriesMode.PREFER_PROJECT)
   repositories {
      google()
      mavenCentral()
      maven { url 'https://maven.pkg.jetbrains.space/public/p/compose/dev' }
   }

   versionCatalogs {
      libs {
         version('kotlin', '1.7.20')
         version('android-plugin', '7.3.1')
         version('compose', '1.3.0')
         version('accompanist', '0.28.0')

         plugin('kotlin-multiplatform', 'org.jetbrains.kotlin.multiplatform').versionRef('kotlin')
         plugin('android-application', 'com.android.application').versionRef('android-plugin')
         plugin('android-library', 'com.android.library').versionRef('android-plugin')
         plugin('compose', 'org.jetbrains.compose').versionRef('compose')
         plugin('kotlinx-serialization', 'org.jetbrains.kotlin.plugin.serialization').versionRef('kotlin')

         library('kotlinx-collections-immutable', 'org.jetbrains.kotlinx', 'kotlinx-collections-immutable').version('0.3.5')
         library('kotlinx-datetime', 'org.jetbrains.kotlinx', 'kotlinx-datetime').version('0.4.0')

         library('android-desugaring-jdk', 'com.android.tools', 'desugar_jdk_libs').version('1.2.2')

         library('androidx-activity-compose', 'androidx.activity', 'activity-compose').version('1.6.1')
         library('accompanist-pager', 'com.google.accompanist', 'accompanist-pager').versionRef('accompanist')

         library('kotlinx-serialization', 'org.jetbrains.kotlinx', 'kotlinx-serialization-json').version('1.4.1')

         library('kotlin-test', 'org.jetbrains.kotlin', 'kotlin-test').versionRef('kotlin')
         library('androidx-test-ext', 'androidx.test.ext:junit:1.1.5')
         library('androidx-test-espresso', 'androidx.test.espresso:espresso-core:3.5.1')
         library('mockk', 'io.mockk', 'mockk').version('1.13.4')
      }
   }
}

rootProject.name = "Probosqis"
include ':androidApp'
include ':desktopApp'
// include ':jsApp'
include ':modules:app'
include ':modules:ext:kotlin'
include ':modules:ext:android'
include ':modules:ext:compose'
include ':modules:cache:core'
include ':modules:cache:compose'
include ':modules:page:core'
include ':modules:page:perpetuation'
include ':modules:page:compose'
